; winiio.as: ini file i/o for windows app
; written by gocha, feel free to redistribute


#ifndef WATCH_INIIO_INCLUDED
#define global WATCH_INIIO_INCLUDED

#include "wport.hsp"

goto *WATCH_INIIO_EOF

;---------------------------------------------------------------

#module Watch_IniIO

#deffunc ini_init
	ini_buf_max = 64 * 1024
	sdim ini_path, PATH_MAX
	sdim ini_buf, ini_buf_max
return

#deffunc ini_setfile str _p1
	GetFullPathName _p1, PATH_MAX, varptr(ini_path), NULL
return

#defcfunc ini_getint str _p1, str _p2, int _p3
	GetPrivateProfileInt _p1, _p2, _p3, ini_path : ret = stat
return ret

#defcfunc ini_getstr str _p1, str _p2, str _p3
	GetPrivateProfileString _p1, _p2, _p3, varptr(ini_buf), ini_buf_max, ini_path
return ini_buf

#defcfunc ini_getbool str _p1, str _p2, int _p3
	if _p3 {
		GetPrivateProfileString _p1, _p2, "true", varptr(ini_buf), ini_buf_max, ini_path
	} else {
		GetPrivateProfileString _p1, _p2, "false", varptr(ini_buf), ini_buf_max, ini_path
	}

	ini_buf = getpath(ini_buf, 16)
	if (ini_buf == "true") | (ini_buf == "yes") | (ini_buf == "on") {
		return true
	}
	else: if (ini_buf == "false") | (ini_buf == "no") | (ini_buf == "off") {
		return false
	}
	else {
		return int(ini_buf)
	}

#deffunc ini_setint str _p1, str _p2, int _p3
	WritePrivateProfileString _p1, _p2, str(_p3), ini_path
return

#deffunc ini_setstr str _p1, str _p2, str _p3
	WritePrivateProfileString _p1, _p2, _p3, ini_path
return

#deffunc ini_setbool str _p1, str _p2, int _p3
	if (_p3) {
		WritePrivateProfileString _p1, _p2, "true", ini_path
	}
	else {
		WritePrivateProfileString _p1, _p2, "false", ini_path
	}
return

#global

;---------------------------------------------------------------
*WATCH_INIIO_EOF
	ini_init
#endif // !WATCH_INIIO_INCLUDED
